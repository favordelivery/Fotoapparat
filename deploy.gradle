apply plugin: 'maven-publish'

ext {
    publishedGroupId = 'io.fotoapparat'
}

group = publishedGroupId
version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = artifact

                from components.release
                artifact javadocJar
                artifact sourcesJar
                pom {
                    name = libraryName
                    description = libraryDescription
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = project.uri("https://maven.pkg.github.com/favordelivery/Fotoapparat")
                credentials(PasswordCredentials)
            }
        }
    }
}
